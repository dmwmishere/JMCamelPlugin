plugins {
    id 'java'
    id 'maven-publish'
}

group 'org.dmwm.jmeter'
version '0.8'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    //Jmeter stuff:
    compileOnly "org.apache.jmeter:ApacheJMeter_components:${jmeterVersion}"
    compileOnly "org.apache.jmeter:ApacheJMeter_java:${jmeterVersion}"

    // Camel stuff:
    compile "org.apache.camel:camel-core:${camelVersion}"
    compile "org.picocontainer:picocontainer:2.15"
    compile "org.reflections:reflections:0.9.10"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.9.3"

    compileOnly "org.projectlombok:lombok:1.16.20"
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.apache.jmeter:ApacheJMeter_components:${jmeterVersion}"

}

jar {
    from('src/main/java') {
        include '**/*.properties'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task runScenario(type: JavaExec) {
    dependsOn classes
    def jmeter_home_path = System.env.JMETER_HOME
    sourceSets.main.resources.srcDir { 'src/main/java' }
    main = "org.apache.jmeter.NewDriver"
    workingDir = "$jmeter_home_path/bin"
    doFirst {
        classpath = files(
                "$jmeter_home_path/lib/ext/*",
                "$jmeter_home_path/lib/*",
                "$jmeter_home_path/bin/ApacheJMeter.jar"
        )
        classpath += sourceSets.main.runtimeClasspath
    }

    jvmArgs "-DJMETER_HOME=$jmeter_home_path"

    args "-t", "$projectDir/scenario/TestPlugin.jmx"
}

task runIt(dependsOn: jar) {
    def jmeter_home = "$System.env.JMETER_HOME"
    doFirst {
        if (System.env.JMETER_HOME == null || System.env.JAVA_HOME == null) {
            throw new GradleException('Either JMETER_HOME or JAVA_HOME not specified!')
        }
    }
    doLast {
        copy {
            from "$buildDir/libs/"
            into "$jmeter_home/lib/ext/"
        }
    }
    doLast {
        exec {
            environment JAVA_HOME: "$System.env.JAVA_HOME"
            environment JVM_ARGS: "-Dbean_class_path=org.dmwm.jmeter.beans"

            executable "$jmeter_home/bin/jmeter"
            args "-t", "$projectDir/scenario/TestPlugin.jmx"
        }
    }
}

test {
    exclude '**/*TestCamelConfig*'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'CamelPlugin'
            from components.java
            pom {
                name = 'JMeter Camel plugin'
                description = 'Use of camel context within jmeter tests'
                url = 'http://github.com/dmwmishere/JMCamelPlugin'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'dmwmishere'
                        name = 'Stanislaw'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dmwmishere/JMCamelPlugin.git'
                    url = 'http://github.com/dmwmishere/JMCamelPlugin'
                }
            }
        }
    }
}